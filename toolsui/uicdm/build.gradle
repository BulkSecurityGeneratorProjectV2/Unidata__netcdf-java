plugins {
  id 'com.github.johnrengelman.shadow' version '6.1.0'
}

description = 'Provides a graphical interface to the CDM library.'
ext.title = 'ToolsUI'

apply from: "$rootDir/gradle/any/dependencies.gradle"
apply from: "$rootDir/gradle/any/java-library.gradle"
//apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
  compile enforcedPlatform(project(':netcdf-java-platform'))
  testImplementation enforcedPlatform(project(':netcdf-java-testing-platform'))

  // todo: a lot of these could probably be runtimeOnly
  compile project(':cdm-core')
  compile project(':bufr')
  compile project(':grib')
  compile project(':httpservices')
  runtime project(':netcdf4')
  compile project(':opendap')
  compile project(':uibase')

  // Used by NcmlEditor. Abandoned, no updates since 2013. https://sourceforge.net/projects/bounce/
  // https://sourceforge.net/p/bounce/svn/HEAD/tree/src/main/java/org/bounce/
  compile 'org.bounce:bounce:0.18'

  // LGoodDatePicker - swing calendar widget used in TdsMonitor
  compile 'com.github.lgooddatepicker:LGoodDatePicker:10.3.1'

  // http://www.jfree.org/
  compile 'org.jfree:jcommon:1.0.23'
  compile 'org.jfree:jfreechart:1.0.19'

  // constrained by netcdf-java-platform
  compile 'org.jdom:jdom2'
  compile 'org.apache.httpcomponents:httpclient'
  compile 'com.google.re2j:re2j'
  compile 'org.slf4j:slf4j-api'

  testImplementation project(':cdm-test-utils')
  testImplementation 'junit:junit'

  runtimeOnly project(':gcdm')
  runtimeOnly project(':cdm-s3')

  // constrained by netcdf-java-platform
  runtimeOnly 'ch.qos.logback:logback-classic'
}

/////////////////
// toolsUI jar

shadowJar {
  archiveBaseName.set('toolsUI')
  archiveClassifier.set('')
  group = 'shadow'

  // Filter out crap from various other packages.
  exclude 'AUTHORS'
  exclude 'DATE'
  exclude 'LICENCE'
  exclude 'LICENSE'
  exclude 'NOTICE'
  exclude '*.txt'
  exclude 'META-INF/INDEX.LIST'
  exclude 'META-INF/DEPENDENCIES'
  exclude 'META-INF/LICENSE'
  exclude 'META-INF/NOTICE'
  exclude 'META-INF/*.SF'
  exclude 'META-INF/*.DSA'
  exclude 'META-INF/*.RSA'
  exclude 'META-INF/*.txt'
  exclude 'META-INF/*.xml'

  // Transformations
  append('META-INF/spring.handlers')
  append('META-INF/spring.schemas')
  mergeServiceFiles()

  manifest.attributes 'Main-Class': 'ucar.nc2.ui.ToolsUI'
}

publishing {
  publications {
    shadow(MavenPublication) { publication ->
      publication.groupId = 'edu.ucar'
      publication.artifactId = 'toolsUI'
      project.shadow.component(publication)
    }
  }
}

// make sure we only end up with one slf4j binding on the classpath
configurations.all {
  resolutionStrategy.dependencySubstitution {
    // Replace 'org.slf4j:slf4j-jdk14' from gcdm with logback-classic
    substitute module('org.slf4j:slf4j-jdk14') because 'we only want one slf4j binding' with module("ch.qos.logback:logback-classic:${depVersion.logback}")
  }
}
